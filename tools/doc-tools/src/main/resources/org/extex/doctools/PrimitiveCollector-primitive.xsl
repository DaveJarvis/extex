<?xml version="1.0" encoding="ISO-8859-1"?>
<!-- 
 Copyright (C) 2008-2011 The ExTeX Group and individual authors listed below

 This library is free software; you can redistribute it and/or modify it
 under the terms of the GNU Lesser General Public License as published by the
 Free Software Foundation; either version 2.1 of the License, or (at your
 option) any later version.

 This library is distributed in the hope that it will be useful, but WITHOUT
 ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
 FITNESS FOR A PARTICULAR PURPOSE. See the GNU Lesser General Public License
 for more details.

 You should have received a copy of the GNU Lesser General Public License
 along with this library; if not, write to the Free Software Foundation,
 Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307 USA

*****************************************************************************
 Author: Gerd Neugebauer
 -->
<!DOCTYPE ent [
  <!ENTITY quot "&#34;">
  <!ENTITY nbsp "&#160;">
  <!ENTITY iexcl "&#161;">
  <!ENTITY cent "&#162;">
  <!ENTITY pound "&#163;">
  <!ENTITY curren "&#164;">
  <!ENTITY yen "&#165;">
  <!ENTITY brvbar "&#166;">
  <!ENTITY sect "&#167;">
  <!ENTITY uml "&#168;">
  <!ENTITY copy "&#169;">
  <!ENTITY ordf "&#170;">
  <!ENTITY laquo "&#171;">
  <!ENTITY not "&#172;">
  <!ENTITY shy "&#173;">
  <!ENTITY reg "&#174;">
  <!ENTITY macr "&#175;">
  <!ENTITY deg "&#176;">
  <!ENTITY plusmn "&#177;">
  <!ENTITY sup2 "&#178;">
  <!ENTITY sup3 "&#179;">
  <!ENTITY acute "&#180;">
  <!ENTITY micro "&#181;">
  <!ENTITY para "&#182;">
  <!ENTITY middot "&#183;">
  <!ENTITY cedil "&#184;">
  <!ENTITY sup1 "&#185;">
  <!ENTITY ordm "&#186;">
  <!ENTITY raquo "&#187;">
  <!ENTITY frac14 "&#188;">
  <!ENTITY frac12 "&#189;">
  <!ENTITY frac34 "&#190;">
  <!ENTITY iquest "&#191;">
  <!ENTITY Agrave "&#192;">
  <!ENTITY Aacute "&#193;">
  <!ENTITY Acirc "&#194;">
  <!ENTITY Atilde "&#195;">
  <!ENTITY Auml "&#196;">
  <!ENTITY Aring "&#197;">
  <!ENTITY AElig "&#198;">
  <!ENTITY Ccedil "&#199;">
  <!ENTITY Egrave "&#200;">
  <!ENTITY Eacute "&#201;">
  <!ENTITY Ecirc "&#202;">
  <!ENTITY Euml "&#203;">
  <!ENTITY Igrave "&#204;">
  <!ENTITY Iacute "&#205;">
  <!ENTITY Icirc "&#206;">
  <!ENTITY Iuml "&#207;">
  <!ENTITY ETH "&#208;">
  <!ENTITY Ntilde "&#209;">
  <!ENTITY Ograve "&#210;">
  <!ENTITY Oacute "&#211;">
  <!ENTITY Ocirc "&#212;">
  <!ENTITY Otilde "&#213;">
  <!ENTITY Ouml "&#214;">
  <!ENTITY times "&#215;">
  <!ENTITY Oslash "&#216;">
  <!ENTITY Ugrave "&#217;">
  <!ENTITY Uacute "&#218;">
  <!ENTITY Ucirc "&#219;">
  <!ENTITY Uuml "&#220;">
  <!ENTITY Yacute "&#221;">
  <!ENTITY THORN "&#222;">
  <!ENTITY szlig "&#223;">
  <!ENTITY agrave "&#224;">
  <!ENTITY aacute "&#225;">
  <!ENTITY acirc "&#226;">
  <!ENTITY atilde "&#227;">
  <!ENTITY auml "&#228;">
  <!ENTITY aring "&#229;">
  <!ENTITY aelig "&#230;">
  <!ENTITY ccedil "&#231;">
  <!ENTITY egrave "&#232;">
  <!ENTITY eacute "&#233;">
  <!ENTITY ecirc "&#234;">
  <!ENTITY euml "&#235;">
  <!ENTITY igrave "&#236;">
  <!ENTITY iacute "&#237;">
  <!ENTITY icirc "&#238;">
  <!ENTITY iuml "&#239;">
  <!ENTITY eth "&#240;">
  <!ENTITY ntilde "&#241;">
  <!ENTITY ograve "&#242;">
  <!ENTITY oacute "&#243;">
  <!ENTITY ocirc "&#244;">
  <!ENTITY otilde "&#245;">
  <!ENTITY ouml "&#246;">
  <!ENTITY divide "&#247;">
  <!ENTITY oslash "&#248;">
  <!ENTITY ugrave "&#249;">
  <!ENTITY uacute "&#250;">
  <!ENTITY ucirc "&#251;">
  <!ENTITY uuml "&#252;">
  <!ENTITY yacute "&#253;">
  <!ENTITY thorn "&#254;">
  <!ENTITY yuml "&#255;">
  <!ENTITY Alpha "&#913;">
  <!ENTITY alpha "&#945;">
  <!ENTITY Beta "&#914;">
  <!ENTITY beta "&#946;">
  <!ENTITY Gamma "&#915;">
  <!ENTITY gamma "&#947;">
  <!ENTITY Delta "&#916;">
  <!ENTITY delta "&#948;">
  <!ENTITY Epsilon "&#917;">
  <!ENTITY epsilon "&#949;">
  <!ENTITY Zeta "&#918;">
  <!ENTITY zeta "&#950;">
  <!ENTITY Eta "&#919;">
  <!ENTITY eta "&#951;">
  <!ENTITY Theta "&#920;">
  <!ENTITY theta "&#952;">
  <!ENTITY Iota "&#921;">
  <!ENTITY iota "&#953;">
  <!ENTITY Kappa "&#922;">
  <!ENTITY kappa "&#954;">
  <!ENTITY Lambda "&#923;">
  <!ENTITY lambda "&#955;">
  <!ENTITY Mu "&#924;">
  <!ENTITY mu "&#956;">
  <!ENTITY Nu "&#925;">
  <!ENTITY nu "&#957;">
  <!ENTITY Xi "&#926;">
  <!ENTITY xi "&#958;">
  <!ENTITY Omicron "&#927;">
  <!ENTITY omicron "&#959;">
  <!ENTITY Pi "&#928;">
  <!ENTITY pi "&#960;">
  <!ENTITY Rho "&#929;">
  <!ENTITY rho "&#961;">
  <!ENTITY Sigma "&#931;">
  <!ENTITY sigmaf "&#962;">
  <!ENTITY sigma "&#963;">
  <!ENTITY Tau "&#932;">
  <!ENTITY tau "&#964;">
  <!ENTITY Upsilon "&#933;">
  <!ENTITY upsilon "&#965;">
  <!ENTITY Phi "&#934;">
  <!ENTITY phi "&#966;">
  <!ENTITY Chi "&#935;">
  <!ENTITY chi "&#967;">
  <!ENTITY Psi "&#936;">
  <!ENTITY psi "&#968;">
  <!ENTITY Omega "&#937;">
  <!ENTITY omega "&#969;">
  <!ENTITY thetasym "&#977;">
  <!ENTITY upsih "&#978;">
  <!ENTITY piv "&#982;">
  <!ENTITY forall "&#8704;">
  <!ENTITY part "&#8706;">
  <!ENTITY exist "&#8707;">
  <!ENTITY empty "&#8709;">
  <!ENTITY nabla "&#8711;">
  <!ENTITY isin "&#8712;">
  <!ENTITY notin "&#8713;">
  <!ENTITY ni "&#8715;">
  <!ENTITY prod "&#8719;">
  <!ENTITY sum "&#8721;">
  <!ENTITY minus "&#8722;">
  <!ENTITY lowast "&#8727;">
  <!ENTITY radic "&#8730;">
  <!ENTITY prop "&#8733;">
  <!ENTITY infin "&#8734;">
  <!ENTITY ang "&#8736;">
  <!ENTITY and "&#8743;">
  <!ENTITY or "&#8744;">
  <!ENTITY cap "&#8745;">
  <!ENTITY cup "&#8746;">
  <!ENTITY int "&#8747;">
  <!ENTITY there4 "&#8756;">
  <!ENTITY sim "&#8764;">
  <!ENTITY cong "&#8773;">
  <!ENTITY asymp "&#8776;">
  <!ENTITY ne "&#8800;">
  <!ENTITY equiv "&#8801;">
  <!ENTITY le "&#8804;">
  <!ENTITY ge "&#8805;">
  <!ENTITY sub "&#8834;">
  <!ENTITY sup "&#8835;">
  <!ENTITY nsub "&#8836;">
  <!ENTITY sube "&#8838;">
  <!ENTITY supe "&#8839;">
  <!ENTITY oplus "&#8853;">
  <!ENTITY otimes "&#8855;">
  <!ENTITY perp "&#8869;">
  <!ENTITY sdot "&#8901;">
  <!ENTITY loz "&#9674;">
  <!ENTITY lceil "&#8968;">
  <!ENTITY rceil "&#8969;">
  <!ENTITY lfloor "&#8970;">
  <!ENTITY rfloor "&#8971;">
  <!ENTITY lang "&#9001;">
  <!ENTITY rang "&#9002;">
  <!ENTITY larr "&#8592;">
  <!ENTITY uarr "&#8593;">
  <!ENTITY rarr "&#8594;">
  <!ENTITY darr "&#8595;">
  <!ENTITY harr "&#8596;">
  <!ENTITY crarr "&#8629;">
  <!ENTITY lArr "&#8656;">
  <!ENTITY uArr "&#8657;">
  <!ENTITY rArr "&#8658;">
  <!ENTITY dArr "&#8659;">
  <!ENTITY hArr "&#8660;">
  <!ENTITY bull "&#8226;">
  <!ENTITY prime "&#8242;">
  <!ENTITY oline "&#8254;">
  <!ENTITY frasl "&#8260;">
  <!ENTITY weierp "&#8472;">
  <!ENTITY image "&#8465;">
  <!ENTITY real "&#8476;">
  <!ENTITY trade "&#8482;">
  <!ENTITY euro "&#8364;">
  <!ENTITY alefsym "&#8501;">
  <!ENTITY spades "&#9824;">
  <!ENTITY clubs "&#9827;">
  <!ENTITY hearts "&#9829;">
  <!ENTITY diams "&#9830;">
  <!ENTITY ensp "&#8194;">
  <!ENTITY emsp "&#8195;">
  <!ENTITY thinsp "&#8201;">
  <!ENTITY zwnj "&#8204;">
  <!ENTITY zwj "&#8205;">
  <!ENTITY lrm "&#8206;">
  <!ENTITY rlm "&#8207;">
  <!ENTITY ndash "&#8211;">
  <!ENTITY mdash "&#8212;">
  <!ENTITY lsquo "&#8216;">
  <!ENTITY rsquo "&#8217;">
  <!ENTITY sbquo "&#8218;">
  <!ENTITY ldquo "&#8220;">
  <!ENTITY rdquo "&#8221;">
  <!ENTITY bdquo "&#8222;">
  <!ENTITY dagger "&#8224;">
  <!ENTITY Dagger "&#8225;">
  <!ENTITY hellip "&#8230;">
  <!ENTITY permil "&#8240;">
  <!ENTITY lsaquo "&#8249;">
  <!ENTITY rsaquo "&#8250;">
]>

<!-- ====================================================================== -->
<xsl:stylesheet version="1.0"
                xmlns:pom="http://maven.apache.org/POM/4.0.0"
                xmlns:xsl="http://www.w3.org/1999/XSL/Transform">
  <xsl:output encoding="iso-8859-1" method="text"/>

  <!-- ===================================================================== -->
  <xsl:template match="/primitives">
\begin{primitives}<xsl:apply-templates select="primitive/doc[@type='register' or @type='primitive']"/>\end{primitives}
</xsl:template>

  <!-- ===================================================================== -->
  <xsl:template match="doc">
\begin{doc}{<xsl:value-of select="@class"/>}
<xsl:apply-templates select="*|text()"/>
\end{doc}</xsl:template>
  <!-- ===================================================================== -->
  <xsl:template match="link|linkplain"><xsl:apply-templates select="*|text()"/></xsl:template>
  <!-- ===================================================================== -->
  <xsl:template match="p">

<xsl:apply-templates select="text()|*"/>

</xsl:template>
  <!-- ===================================================================== -->
  <xsl:template match="br">\\ </xsl:template>
  <!-- ===================================================================== -->
  <xsl:template match="b">\textbf{<xsl:apply-templates select="text()|*"/>}</xsl:template>
  <!-- ===================================================================== -->
  <xsl:template match="i">\textit{<xsl:apply-templates select="text()|*"/>}</xsl:template>
  <!-- ===================================================================== -->
  <xsl:template match="em">\emph{<xsl:apply-templates select="text()|*"/>}</xsl:template>
  <!-- ===================================================================== -->
  <xsl:template match="strong">\emph{<xsl:apply-templates select="text()|*"/>}</xsl:template>
  <!-- ===================================================================== -->
  <xsl:template match="code">\texttt{<xsl:apply-templates select="text()|*"/>}</xsl:template>
  <!-- ===================================================================== -->
  <xsl:template match="tt"><xsl:variable name="x"><xsl:value-of select="."
  /></xsl:variable><xsl:choose>
    <xsl:when test="$x='\ '">\Macro{\[}</xsl:when>
    <xsl:when test="$x='\\'">\Macro{\char`\\}</xsl:when>
    <xsl:when test="starts-with($x,'\')">\Macro{<xsl:value-of select="substring($x,2)"/>}</xsl:when>
    <xsl:when test="$x='{'">\texttt{\char`\{}</xsl:when>
    <xsl:when test="$x='}'">\texttt{\char`\}}</xsl:when>
    <xsl:otherwise>\texttt{<xsl:value-of select="$x"/>}</xsl:otherwise>
  </xsl:choose></xsl:template>
  <!-- ===================================================================== -->
  <xsl:template match="sub">\ensuremath{_{<xsl:apply-templates select="text()|*"/>}}</xsl:template>
  <!-- ===================================================================== -->
  <xsl:template match="sup">\ensuremath{^{<xsl:apply-templates select="text()|*"/>}}</xsl:template>
  <!-- ===================================================================== -->
  <xsl:template match="h1">\part{<xsl:apply-templates select="text()|*"/>}</xsl:template>
  <!-- ===================================================================== -->
  <xsl:template match="h2">\section{<xsl:apply-templates select="text()|*"/>}</xsl:template>
  <!-- ===================================================================== -->
  <xsl:template match="h3">\subsection{<xsl:apply-templates select="text()|*"/>}</xsl:template>
  <!-- ===================================================================== -->
  <xsl:template match="h4">\subsubsection{<xsl:apply-templates select="text()|*"/>}</xsl:template>
  <!-- ===================================================================== -->
  <xsl:template match="dl">\begin{description}<xsl:apply-templates select="dt|dd"/>
\end{description}</xsl:template>
  <!-- ===================================================================== -->
  <xsl:template match="dt">
  \item{<xsl:apply-templates select="*|text()"/>}</xsl:template>
  <!-- ===================================================================== -->
  <xsl:template match="dd"><xsl:apply-templates select="*|text()"/></xsl:template>
  <!-- ===================================================================== -->
  <xsl:template match="ul">\begin{itemize}<xsl:apply-templates select="li"/>
\end{itemize}</xsl:template>
  <!-- ===================================================================== -->
  <xsl:template match="ol">\begin{enumerate}<xsl:apply-templates select="li"/>
\end{enumerate}</xsl:template>
  <!-- ===================================================================== -->
  <xsl:template match="li">
  \item <xsl:apply-templates select="text()|*"/>
  </xsl:template>
  <!-- ===================================================================== -->
  <xsl:template match="table">\begin{tabular}{<xsl:variable 
    name="x"><xsl:value-of select="@format"/></xsl:variable><xsl:choose>
    <xsl:when test="string-length($x)=0"
     ><xsl:value-of select="substring('llllllllllllllllllllllllllllll',1,count(tr[position()=1]/td))"/></xsl:when>
    <xsl:otherwise><xsl:value-of select="$x"/></xsl:otherwise>
    </xsl:choose>}  
  <xsl:apply-templates select="tr"/> \end{tabular}
  </xsl:template>
  <!-- ===================================================================== -->
  <xsl:template match="tr"><xsl:apply-templates select="td"/>\\
  </xsl:template>
  <!-- ===================================================================== -->
  <xsl:template match="td"><xsl:if test="position()>1">&amp;</xsl:if><xsl:apply-templates select="*|text()"/></xsl:template>
  <!-- ===================================================================== -->
  <xsl:template match="logo"><xsl:variable name="x"><xsl:value-of select="text()|*"/></xsl:variable><xsl:choose>
  <xsl:when test="$x='Omega'">\ensuremath{\Omega}</xsl:when>
  <xsl:otherwise>\<xsl:value-of select="$x"/>{}</xsl:otherwise></xsl:choose></xsl:template>
  <!-- ===================================================================== -->
  <xsl:template match="todo">
  \Incomplete{<xsl:value-of select="text()|*"/>}
</xsl:template>
  <!-- ===================================================================== -->
  <xsl:template match="pre[@class='syntax']">\begin{syntax}<xsl:apply-templates select="text()|*" mode="syntax"/>\end{syntax}</xsl:template>
  <!-- ===================================================================== -->
  <xsl:template match="pre[@class='TeXSample']">\begin{lstlisting}[language=TeX]<xsl:call-template name="translateTeX"
       ><xsl:with-param name="in"><xsl:copy-of select="text()|*"
       /></xsl:with-param></xsl:call-template>\end{lstlisting}</xsl:template>
  <!-- ===================================================================== -->
  <xsl:template match="pre[@class='JavaSample']">\begin{lstlisting}[language=Java]<xsl:apply-templates mode="java" select="text()|*"/>\end{lstlisting}</xsl:template>
  <!-- ===================================================================== -->
  <xsl:template match="pre[@class='configuration']">\begin{lstlisting}[language=XML]<xsl:copy-of select="text()|*"/>\end{lstlisting}</xsl:template>
  <!-- ===================================================================== -->
  <xsl:template match="pre">\begin{verbatim}<xsl:copy-of select="text()|*"/>\end{verbatim}</xsl:template>
  <!-- ===================================================================== -->
  <xsl:template match="small">{\small <xsl:apply-templates select="text()|*"/>}</xsl:template>
  <!-- ===================================================================== -->
  <xsl:template match="span">{<xsl:apply-templates select="text()|*"/>}</xsl:template>
  <!-- ===================================================================== -->
  <xsl:template match="tt" mode="syntax">\texttt{<xsl:apply-templates select="text()|*" mode="syntax"/>}</xsl:template>
  <!-- ===================================================================== -->
  <xsl:template match="text()" mode="syntax"><xsl:call-template name="translateSyntax"
   ><xsl:with-param name="in"><xsl:value-of select="."/></xsl:with-param
   ></xsl:call-template></xsl:template>
  <!-- ===================================================================== -->
  <xsl:template match="text()"><xsl:call-template name="translate"
   ><xsl:with-param name="in"><xsl:value-of select="."/></xsl:with-param
   ></xsl:call-template></xsl:template>
  <!-- ===================================================================== -->
  <xsl:template match="*">
    <xsl:message terminate="yes">Unhandled node: <xsl:copy-of select="."/>
    </xsl:message>
  </xsl:template>
  <!-- ===================================================================== -->
  <xsl:template match="b" mode="java"><xsl:apply-templates select="text()|*"/></xsl:template>
  <!-- ===================================================================== -->
  <xsl:template match="i" mode="java"><xsl:apply-templates select="text()|*"/></xsl:template>
  <!-- ===================================================================== -->
  <xsl:template match="text()" mode="java"><xsl:value-of select="."/></xsl:template>
  <!-- ===================================================================== -->
  <xsl:template name="translate">
    <xsl:param name="in"/>
    <xsl:if test="string-length($in)!=0">

     <xsl:choose>
      <xsl:when test="starts-with($in,'\')">\textbackslash{}</xsl:when>
      <xsl:when test="starts-with($in,'_')">\_</xsl:when>
      <xsl:when test="starts-with($in,'$')">\$</xsl:when>
      <xsl:when test="starts-with($in,'~')">\texttilde </xsl:when>
      <xsl:when test="starts-with($in,'&nbsp;')">~</xsl:when>
      <xsl:when test="starts-with($in,'&ndash;')">--</xsl:when>
      <xsl:when test="starts-with($in,'&Omega;')">\ensuremath{\Omega}</xsl:when>
      <xsl:when test="starts-with($in,'&alpha;')">\ensuremath{\alpha}</xsl:when>
      <xsl:when test="starts-with($in,'&beta;')">\ensuremath{\beta}</xsl:when>
      <xsl:when test="starts-with($in,'&lang;')">\tag{</xsl:when>
      <xsl:when test="starts-with($in,'&rang;')">}</xsl:when>
      <xsl:otherwise><xsl:value-of select="substring($in,1,1)"
       /></xsl:otherwise>
     </xsl:choose>
     <xsl:call-template name="translate"
       ><xsl:with-param name="in"><xsl:value-of select="substring($in,2)"
       /></xsl:with-param></xsl:call-template></xsl:if>
  </xsl:template>

  <!-- ===================================================================== -->
  <xsl:template name="translateSyntax">
    <xsl:param name="in"/>
    <xsl:if test="string-length($in) != 0">
     <xsl:choose>
      <xsl:when test="starts-with($in,'&#10;')"> \\&#10;</xsl:when>
      <xsl:when test="starts-with($in,'\')">\textbackslash{}</xsl:when>
      <xsl:when test="starts-with($in,'_')">\_</xsl:when>
      <xsl:when test="starts-with($in,'$')">\$</xsl:when>
      <xsl:when test="starts-with($in,'~')">\texttilde </xsl:when>
      <xsl:when test="starts-with($in,'&nbsp;')">~</xsl:when>
      <xsl:when test="starts-with($in,'&ndash;')">--</xsl:when>
      <xsl:when test="starts-with($in,'|')">\SyntaxOr </xsl:when>
      <xsl:when test="starts-with($in,'&lang;')">\tag{</xsl:when>
      <xsl:when test="starts-with($in,'&rang;')">}</xsl:when>
      <xsl:when test="starts-with($in,'&rarr;')">\SyntaxDef </xsl:when>
      <xsl:when test="starts-with($in,'&Omega;')">\ensuremath{\Omega}</xsl:when>
      <xsl:when test="starts-with($in,'&alpha;')">\ensuremath{\alpha}</xsl:when>
      <xsl:when test="starts-with($in,'&beta;')">\ensuremath{\beta}</xsl:when>
      <xsl:otherwise><xsl:value-of select="substring($in,1,1)"
       /></xsl:otherwise>
     </xsl:choose>
     <xsl:call-template name="translateSyntax"
       ><xsl:with-param name="in"><xsl:value-of select="substring($in,2)"
       /></xsl:with-param></xsl:call-template></xsl:if>
  </xsl:template>

  <!-- ===================================================================== -->
  <xsl:template name="translateTeX">
    <xsl:param name="in"/>
    <xsl:if test="string-length($in)!=0">
     <xsl:choose>
      <xsl:when test="starts-with($in,'&rArr;')">\ensuremath{\Rightarrow}</xsl:when>
      <xsl:when test="starts-with($in,'&szlig;')">ß</xsl:when>
      <xsl:when test="starts-with($in,'&rarr;')"> --&gt; </xsl:when>
      <xsl:otherwise><xsl:value-of select="substring($in,1,1)"
       /></xsl:otherwise>
     </xsl:choose>
     <xsl:call-template name="translateTeX"
       ><xsl:with-param name="in"><xsl:value-of select="substring($in,2)"
       /></xsl:with-param></xsl:call-template></xsl:if>
  </xsl:template>

</xsl:stylesheet>
